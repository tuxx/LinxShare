plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.plugin.serialization'
	id 'org.jetbrains.kotlin.plugin.compose'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('keystore.properties')
def hasKeystore = keystorePropertiesFile.exists()
if (hasKeystore) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
	namespace = 'com.kolktech.linxshare'
	compileSdk 36

	defaultConfig {
		applicationId "com.kolktech.linxshare"
		minSdk 23
		targetSdk 36
		versionCode 21
		versionName "1.5"
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	// Exclude dependency metadata to improve reproducibility
	dependenciesInfo {
		includeInApk = false
		includeInBundle = false
	}

	if (hasKeystore) {
		signingConfigs {
			release {
				storeFile file(keystoreProperties['storeFile'])
				storePassword keystoreProperties['storePassword']
				keyAlias keystoreProperties['keyAlias']
				keyPassword keystoreProperties['keyPassword']
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			if (hasKeystore) {
				signingConfig signingConfigs.release
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	buildFeatures {
		compose = true
	}
}

if (project.hasProperty('PUBLISH_PLAY')) {
    apply plugin: 'com.github.triplet.play'
    play {
        serviceAccountCredentials.set(file('play-service-account.json'))
        track.set('internal')
        defaultToAppBundles.set(true)
    }
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation 'androidx.core:core-ktx:1.17.0'
	implementation 'com.google.android.material:material:1.13.0'
	implementation platform('androidx.compose:compose-bom:2026.02.01')
	implementation 'androidx.activity:activity-compose:1.12.4'
	implementation 'androidx.datastore:datastore-preferences:1.2.0'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
	implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.10.0'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
	implementation 'androidx.compose.ui:ui'
	implementation 'androidx.compose.ui:ui-tooling-preview'
	implementation 'androidx.compose.foundation:foundation'
	implementation 'androidx.compose.material3:material3'
	implementation 'com.squareup.okhttp3:okhttp:5.3.2'
	implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0'
	testImplementation 'junit:junit:4.13.2'
	debugImplementation 'androidx.compose.ui:ui-tooling'
	debugImplementation 'androidx.compose.ui:ui-test-manifest'
	androidTestImplementation 'androidx.test.ext:junit:1.3.0'
	androidTestImplementation 'androidx.test:core:1.7.0'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}
